import com.module.plugin.Alibaba
import com.module.plugin.BuildConfig

plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.module.build.plugin'
}

if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace "com.module.me"
    compileSdk BuildConfig.compileSdk
    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.module.me"
            versionCode BuildConfig.versionCode
            versionName BuildConfig.versionName
        }
        minSdk BuildConfig.minSdk
        targetSdk BuildConfig.targetSdk
    }

    buildTypes {
        debug {
            // minifyEnabled是否启用混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            // minifyEnabled是否启用混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    resourcePrefix "me_"

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java {
                    //全部Module一起编译的时候剔除debug目录
                    exclude '**/**/debug/**'
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig false
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    api project(':commonlib')
    kapt Alibaba.arouter_compiler
}