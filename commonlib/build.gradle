import com.module.plugin.*

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.module.build.plugin'
}

android {
    namespace "com.module.common"
    compileSdk BuildConfig.compileSdk
    defaultConfig {
        minSdk BuildConfig.minSdk
        targetSdk BuildConfig.targetSdk
    }

    buildTypes {
        debug {
            manifestPlaceholders = [
                    GAODE_MAP_KEY: gaode_map_key
            ]
            minifyEnabled false
        }
        release {
            manifestPlaceholders = [
                    GAODE_MAP_KEY: gaode_map_key
            ]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        buildConfig false
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    // 支付宝支付通过导入aar包
    implementation project(':localrepo')
    api project(':baselib')
    api project(':networklib')
    api Google.material
    api Google.zxing
    implementation AndroidX.lifecycle_common_java8
    api AndroidX.viewpager2
    api AndroidX.constraintlayout
    implementation AndroidX.swiperefreshlayout
    // 图片选择器
    api Github.pictureselector
    api Github.snackBar
    // https://github.com/coil-kt/coil/ 图片加载框架
    implementation Github.coil
    implementation Github.glide
    kapt Github.glide_compiler
    // 检测内存泄漏
    debugImplementation Squareup.leakcanary_android
    // 阿里路由
    api Alibaba.arouter
    kapt Alibaba.arouter_compiler
    // 微信支付
    api 'com.tencent.mm.opensdk:wechat-sdk-android:+'
    // 微信支付封装库
    api project(':wxannotaion')
    kapt project(':wxcompiler')
    // 其中latest.release指代最新Bugly SDK版本号
    api 'com.tencent.bugly:crashreport:latest.release'
    // 高德地图定位
    implementation 'com.amap.api:location:latest.integration'
}